/*
 * Copyright 2015-present Facebook, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

{
  parserClass="com.facebook.buck.intellij.ideabuck.lang.BcfgParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Bcfg"
  psiImplClassSuffix="Impl"
  psiPackage="com.facebook.buck.intellij.ideabuck.lang.psi"
  psiImplPackage="com.facebook.buck.intellij.ideabuck.lang.psi.impl"

  elementTypeHolderClass="com.facebook.buck.intellij.ideabuck.lang.psi.BcfgTypes"
  elementTypeClass="com.facebook.buck.intellij.ideabuck.lang.psi.BcfgElementType"
  tokenTypeClass="com.facebook.buck.intellij.ideabuck.lang.psi.BcfgTokenType"

  psiImplUtilClass="com.facebook.buck.intellij.ideabuck.lang.psi.impl.BcfgPsiImplUtil"

  tokens = [
    COMMENT = 'regexp:[#;][^\r\n]*'
    L_BRACKET = '['
    R_BRACKET = ']'
    REQUIRED_INLINE = '<file:'
    OPTIONAL_INLINE = '<?file:'
    END_INLINE = '>'
    ASSIGN = 'regexp:[:=]'
    SECTION_NAME = 'regexp:[A-Za-z_][-A-Za-z0-9_#.]*'
    PROPERTY_NAME = 'regexp:[A-Za-z_][-A-Za-z0-9_#.]*'
    PROPERTY_VALUE_FRAGMENT = 'regexp:[^\\\r\n]*'
    FILE_PATH = 'regexp:[^>\r\n]+'
  ]
}

bcfgFile ::= (section | inline)*

section ::= section_header property*
section_header ::= '[' SECTION_NAME ']'

property ::= PROPERTY_NAME ASSIGN PROPERTY_VALUE_FRAGMENT* {
  methods=[
    getPropertyValueAsText
    getPropertyValueFragmentList
  ]
}

inline ::= (REQUIRED_INLINE | OPTIONAL_INLINE) FILE_PATH END_INLINE
